physics QGSP_BIC
param worldMaterial=Air
param histoFile=beam.root
param inch=25.4
param first=0
param last=300000
param killshld=0
param Bscale=1.
param FramKill=0
g4ui when=4 "/vis/viewer/set/background 1 1 1"

# beam
beam gaussian particle=pi+ firstEvent=$first lastEvent=$last sigmaX=2.0 sigmaY=2.0 beamZ=-500.0 meanMomentum=64000.0
trackcuts keep=pi+,pi-,pi0,kaon+,kaon-,mu+,mu-,e+,e-,gamma,proton,anti_proton

# target
box slab height=31.75 length=209.55 width=6.35 material=Cu color=1,0.01,0.01
place slab rename=slabMinus2 x=+2*(31.75)/5 z=-2*22.145
place slab rename=slabMinus1 x=+1*(31.75)/5 z=-1*22.145
place slab rename=slabCenter  x=0*(31.75)/5  z=0*22.145
place slab rename=slabPlus2  x=-1*(31.75)/5 z=+1*22.145
place slab rename=slabPlus2  x=-2*(31.75)/5 z=+2*22.145

# upstream collimator
group  NewCol
  box  botslab height=5.19*25.4 length=58.00*25.4  width=32.0*25.4  material=Fe color=0,1,1 kill=$killshld
  box  topbot  height=5.19*25.4 length=42.76*25.4  width=32.0*25.4  material=Fe color=0,1,1 kill=$killshld
  box  mid     height=2.00*25.4 length=42.76*25.4  width=11.6*25.4  material=Fe color=0,1,1 kill=$killshld
  trap Coll    height=1160.0 upperWidth=70.0 lowerWidth=150.0 length=2.0*25.4 color=1,0,0 material=Air
  param zmov=7.62*25.4
  place botslab rename=btslab z=0.00  y=-6.19*25.4
  place topbot  rename=bot     z=$zmov  y=-(1.0+5.19/2.0)*25.4
  place mid  rename=midleft    z=$zmov  y=0.0 x=+296/2+67.29  rotation=y-14.03
  place mid  rename=midrigh    z=$zmov  y=0.0 x=-296/2-67.29  rotation=y-17.97
  place topbot  rename=top     z=$zmov  y=(1.0+5.19/2.0)*25.4
endgroup
place NewCol z=(29.0+7.62)*25.4    x=-8.315*25.4+40.0  rotation=y3

# fake detector
virtualdetector StartLine radius=1750.0 length=1 material=Air color=0.9,0.9,0.7
place StartLine rotation=y-13 z=1400.0-26.4 x=-335.0+5.7

# upstream tof counter
param TOFusThick=50.8
virtualdetector TOFus width=150 height=150 length=$TOFusThick material=LUCITE color=0.05,0.05,0.93
place TOFus rotation=z45,y-13 z=1423.0 x=-346.54341

# wire chambers
group ChFram
  box FramS height=254.0 width=63.0  length=25.0 color=1,0,1 kill=$FramKill material=Al
  box FramT height=63.0  width=128.0 length=25.0 color=1,0,1 kill=$FramKill material=Al
  place FramS rename=FrameLeft   z=0.0 x=-95.5
  place FramS rename=FrameRight	 z=0.0 x=+95.5
  place FramT rename=FrameBottom z=0.0 y=-95.5
  place FramT rename=FrameTop	 z=0.0 y=+95.5
endgroup

virtualdetector Det  width=128.0  height=125.0 color=0,1,0  length=25.0
place Det rename=Det1   z=1730.3369 x=-403.0472 y=0.0508 rotation=y-13
place ChFram            z=1730.3369 x=-403.0472 y=0.0508 rotation=y-13
place Det rename=Det2   z=3181.9215 x=-738.0351 y=0.0762 rotation=y-13
place ChFram            z=3181.9215 x=-738.0351 y=0.0762 rotation=y-13

# 10 degree bend
genericbend M1 fieldWidth=4*$inch fieldHeight=3.5*$inch fieldLength=42*$inch kill=$killshld ironColor=1,0,0 ironWidth=42*$inch ironHeight=28*$inch ironLength=42*$inch
place M1 By=1.8*$Bscale rotation=Y-13+5 z=4228.9 x=-945.7

# wire chamber
place Det rename=Det3 z=5167.5665 x=-1064.133 y=-2.921 rotation=y-3
place ChFram          z=5167.5665 x=-1064.133 y=-2.921 rotation=y-3

# downstream collimator
group DScoll
  box dstb   height=8.5*$inch length=36*$inch width=30*$inch material=Fe color=0,1,1 kill=$killshld
  box dsside height=6*$inch  length=36*$inch width=11*$inch  material=Fe color=0,.8,1 kill=$killshld

  place dsside rename=dsl  z=0 x=+9*$inch
  place dsside rename=dsr  z=0 x=-9*$inch
  place dstb   rename=dst  z=0  y=+(4.25+3)*$inch
  place dstb   rename=dsb  z=0  y=-(4.25+3)*$inch
endgroup
place DScoll z=6188.0 x=-1077 rotation=y-3.0

# wire chamber
place Det rename=Det4 z=7606.4872 x=-1195.0827 y=-20.4724 rotation=y-3
place ChFram          z=7606.4872 x=-1195.0827 y=-20.4724 rotation=y-3

# downstream tof counter
param TOFdsThick=50.8
param TOFdsHeight=130.0
virtualdetector TOFds width=$TOFdsHeight height=$TOFdsHeight length=$TOFdsThick material=LUCITE color=0.05,0.05,0.93
place TOFds rename=TOFds z=8005.9022  x=-1186.1546 rotation=z90,y-3

# detector
# box LarDet color=.5,.5,1 height=400.0 width=470.0 length=900.0
# place LarDet z=$B2z+2600.0+1400.0 x=-1150
